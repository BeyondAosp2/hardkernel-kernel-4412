# Copyright:
# ----------------------------------------------------------------------------
# This confidential and proprietary software may be used only as authorized 
# by a licensing agreement from ARM Limited.
#      (C) COPYRIGHT 2010-2011 ARM Limited, ALL RIGHTS RESERVED
# The entire notice above must be reproduced on all authorized copies and 
# copies may only be made to the extent permitted by a licensing agreement
# from ARM Limited.
# ----------------------------------------------------------------------------
#

import os
Import('env')

# Clone the environment so changes don't affect other build files
env_ump = env.Clone()

# Abort building of the device driver for user-mode builds. This is a stop-gap
# until we can build the ump common device driver source for user-mode.
if env_ump['hw'] != '2':
   Return()

if env_ump['v'] != '1':
	env_ump['MAKECOMSTR'] = '[MAKE] ${SOURCE.dir}'

# Source files required for UMP.
ump_src = [Glob('#ump/src/devicedrv/linux/*.c'), Glob('#ump/src/devicedrv/common/*.c')]

env_ump.Append( CPPPATH='#ump/src/devicedrv/' ) 

# Note: cleaning via the Linux kernel build system does not yet work
if env_ump.GetOption('clean') :
	makeAction=Action("cd ${SOURCE.dir}/.. && make clean", '$MAKECOMSTR')
else:
	if env['arch'] == 'x86_32' :
		env_ump['arch_linux'] = 'x86'
	elif 'arm' in env['arch']:
		env_ump['arch_linux'] = 'arm'
	else:
		env_ump['arch_linux'] = env['arch']
	makeAction=Action("cd ${SOURCE.dir}/.. && make ARCH=$arch_linux MALI_HW_TYPE=${hw} MALI_HW_VERSION=${hwver} MALI_DEBUG=${debug} PLATFORM=${platform}  MALI_BASE_TRACK_MEMLEAK=${base_qa}  && cp ump.ko $STATIC_LIB_PATH/ump.ko", '$MAKECOMSTR')
	
# The target is ump.ko, built from the source in ump_src, via the action makeAction
# ump.ko will be copied to $STATIC_LIB_PATH after being built by the standard Linux
# kernel build system, after which it can be installed to the directory specified if
# "libs_install" is set; this is done by LibTarget.
cmd = env_ump.Command('$STATIC_LIB_PATH/ump.ko', ump_src, [makeAction])

env.Depends('$STATIC_LIB_PATH/ump.ko', '$STATIC_LIB_PATH/libosk.a')
# need Module.symvers from ukk.kko
env.Depends('$STATIC_LIB_PATH/ump.ko', '$STATIC_LIB_PATH/ukk.ko')

# Until we fathom out how the invoke the Linux build system to clean, we can use Clean
# to remove generated files.

patterns = ['*.mod.c', '*.o', '*.ko', '*.a', '.*.cmd', 'modules.order', '.tmp_versions', 'Module.symvers']

for p in patterns:
	Clean(cmd, Glob('#ump/src/devicedrv/common/%s' % p))
	Clean(cmd, Glob('#ump/src/devicedrv/linux/%s' % p))
	Clean(cmd, Glob('#ump/src/devicedrv/%s' % p))

env_ump.ProgTarget('ump', cmd)
